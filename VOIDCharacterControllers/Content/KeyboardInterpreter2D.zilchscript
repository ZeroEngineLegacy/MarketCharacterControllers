/************************************************************************************************************/
/* Copyright 2017 DigiPen, All Rights Reserved                                                              */
/* Last Update: 1/4/2017                                                                                    */
/* Author: Zero Engine Team                                                                                 */
/* Last updated by: Arend Danielek                                                                          */
/* Class: KeyboardInterpreter2D                                                                             */
/* Brief: A simple 2D input interpreter for a 2D side-scrolling platformer.                                 */
/************************************************************************************************************/

class CharMoveEvent2D : ZilchEvent
{
    var MoveDir : Real;
    var SprintKeyDown : Boolean = false;
    var JumpKeyPressed : Boolean = false;
}

class KeyboardInterpreter2D : ZilchComponent
{
  sends CharMoveEvent2D : CharMoveEvent2D;

  [Property]
  var LeftKey : Keys = Keys.A;

  [Property]
  var RightKey : Keys = Keys.D;

  [Property]
  var JumpKey : Keys = Keys.Space;

  [Property]
  var SprintKey : Keys = Keys.Shift;

  function Initialize(init : CogInitializer)
  {
    Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
  }

  function OnLogicUpdate(event : UpdateEvent)
  {
    // Determine whether the player wants to move left or right
    var moveDir = 0.0;
    
    if (Zero.Keyboard.KeyIsDown(this.LeftKey))
    {
      moveDir += 1;
    }
    if (Zero.Keyboard.KeyIsDown(this.RightKey))
    {
      moveDir += -1;
    }
    
    var moveEvent = CharMoveEvent2D();
    
    moveEvent.MoveDir = moveDir;
    moveEvent.SprintKeyDown = Zero.Keyboard.KeyIsDown(this.SprintKey);
    moveEvent.JumpKeyPressed = Zero.Keyboard.KeyIsPressed(this.JumpKey);
    
    // Inform the character controller of the player's desired movement
    this.Owner.DispatchEvent(Events.CharMoveEvent2D, moveEvent);
  }
}
