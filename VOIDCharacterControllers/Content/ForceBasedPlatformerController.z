/************************************************************************************************************/
/* Copyright 2016 DigiPen, All Rights Reserved                                                              */
/* Last Update: 12/19/2016                                                                                  */
/* Author: Zero Engine Team                                                                                 */
/* Last updated by: Arend Danielek                                                                          */
/* Class: ForceBasedPlatformerController                                                                       */
/* Brief: Implements a basic force based 2D dynamic side view platformer controller.                         */
/************************************************************************************************************/

class ForceBasedPlatformerController : ZilchComponent
{
    [Dependency]
    var GroundDetection : GroundDetection;
    
    [Dependency]
    var RigidBody : RigidBody;
    
    [Dependency]
    var GravityEffect : GravityEffect;
    
    [Property]
    var MoveForce : Real = 10;
    
    //Multiplier for MoveVelocity when sprinting
    [Property]
    var SprintModifier : Real = 2.0;
    
    //Force to be applied opposite of WorldUp when Jumping
    [Property]
    var JumpForce : Real = 3.0;
    
    [Property]
    var MaxGroundSpeed : Real = 25;
    
    [Property]
    var MaxAirSpeed : Real = 500;
    
    //Mexium number of jumps before the player must land again
    [Property]
    var MaxJumpCount : Integer = 2;
    
    //Current number of jumps
    var JumpCount : Integer = 0;
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Owner, Events.CharMoveEvent, this.OnCharMoveEvent);
        Zero.Connect(this.Owner, Events.CharJumpEvent, this.OnCharJumpEvent);
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        if(this.GroundDetection.IsOnGround || this.GroundDetection.IsPartiallyOnGround)
            this.RigidBody.Velocity = Math.Clamp(this.RigidBody.Velocity, -this.MaxGroundSpeed.XXX, this.MaxGroundSpeed.XXX);
        if(!(this.GroundDetection.IsOnGround || this.GroundDetection.IsPartiallyOnGround))
            this.RigidBody.Velocity = Math.Clamp(this.RigidBody.Velocity, -this.MaxAirSpeed.XXX, this.MaxAirSpeed.XXX);
    }

    function OnCharMoveEvent(event : CharMoveEvent)
    {
        //Console.WriteLine("Received");
        if(this.GroundDetection.IsOnGround || this.GroundDetection.IsPartiallyOnGround)
        {
            var angle = 0.0;
            if(event.MoveDir == -1)
                angle = -Math.Pi/2;
            else if(event.MoveDir == 1)
                angle = Math.Pi/2;
            
            var dir = Math.RotateVector(this.GroundDetection.GroundNormal, Real3(0,0,1), angle);
            this.RigidBody.ApplyForce(dir * this.MoveForce);
        }
    }

    function OnCharJumpEvent(event : CharJumpEvent)
    {
        Console.WriteLine("Jump");
        if(this.GroundDetection.IsOnGround)
        {
            this.JumpCount = 1;
            this.Jump();
        }
        else if(this.JumpCount < this.MaxJumpCount)
        {
            this.JumpCount += 1;
            this.Jump();
        }
    }
    
    function Jump()
    {
        var dir = -this.GravityEffect.Direction;
        this.RigidBody.ApplyForce(dir * this.JumpForce);
    }
}
